
ver = get_option('mri_version')
if ver.version_compare('>1.8') == true
    global_dependencies += dependency('ruby-' + ver)
else
    lib = get_option('ruby_lib')
    global_dependencies += compilers['cpp'].find_library(lib)
    add_project_arguments('-DOLD_RUBY', language: ['cpp','objc','objcpp'])
endif

if get_option('no_preload_scripts') == true
    add_project_arguments('-DNO_PRELOAD_SCRIPTS', language: ['cpp','objc','objcpp'])
endif

global_include_dirs += include_directories('.')

binding_source = [files(
    'binding-mri.cpp',
    'binding-util.cpp',
    'table-binding.cpp',
    'etc-binding.cpp',
    'bitmap-binding.cpp',
    'font-binding.cpp',
    'graphics-binding.cpp',
    'input-binding.cpp',
    'sprite-binding.cpp',
    'viewport-binding.cpp',
    'plane-binding.cpp',
    'window-binding.cpp',
    'tilemap-binding.cpp',
    'audio-binding.cpp',
    'module_rpg.cpp',
    'filesystem-binding.cpp',
    'windowvx-binding.cpp',
    'tilemapvx-binding.cpp',
    'miniffi-binding.cpp'
)]

if discord == true
    binding_source += files('discord-binding.cpp')
endif

global_sources += binding_source